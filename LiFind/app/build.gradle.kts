plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

android {
    namespace = "com.developer27.lifind"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.developer27.lifind"
        minSdk = 26
        targetSdk = 34
        versionCode = 2
        versionName = "1.0.1"

        ndk {
            abiFilters += providers.gradleProperty("lifindTestAbi").orNull
                ?.let { listOf(it) } ?: listOf("armeabi-v7a", "arm64-v8a")
        }

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig = signingConfigs.getByName("debug")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding = true
    }

    // Use the matching C++ runtime from OpenCV, not the obsolete vendored binary.
    sourceSets.getByName("main").jniLibs.setSrcDirs(emptyList<String>())

    androidResources {
        // Ship only the models used by the app; retain training exports in the repository.
        ignoreAssetsPattern = "!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~:old_submission:lifind_new_distance_detection_original_yolo_26l.tflite:lifind_new_led_detection_original_yolo26l.tflite"
        noCompress += "tflite"
    }
}

dependencies {
    // Native runtimes with 16 KB page-size support.
    implementation("org.opencv:opencv:4.14.0")
    implementation("com.google.ai.edge.litert:litert:1.4.1")

    // Kotlin & Android core libs
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("com.google.android.material:material:1.11.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")

    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.6.2")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3")

    // Preferences
    implementation("androidx.preference:preference-ktx:1.2.1")

    //Splash screen
    implementation("androidx.core:core-splashscreen:1.0.0")

    // Testing
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.2.1")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.6.1")
}
